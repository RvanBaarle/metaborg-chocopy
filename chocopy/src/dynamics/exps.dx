module exps

imports
  stdlib
  native
  scope
  
  main
  
  signatures/Expression-sig
  
rules
  compileExp :: 'Exp -> Pluggable(@cval)
  
  compileExp(Not(x)) = {
    v <- compileExp(x)
    if #int-eq(v, int('0)) then {
      after@([int('1)])
    } else {
      after@([int('0)])
    }
  } label after/1:
  compileExp(And(a, b)) = {
    av <- compileExp(a)
    if #int-eq(av, int('0)) then {
      after@([int('0)])
    } else {
      bv <- compileExp(b)
      after@([bv])
    }
  } label after/1:
  compileExp(Or(a, b)) = {
    av <- compileExp(a)
    if #int-eq(av, int('1)) then {
      after@([int('1)])
    } else {
      bv <- compileExp(b)
      after@([bv])
    }
  } label after/1:
  compileExp(Ternary(b, a, c)) = {
    v <- compileExp(a)
    if #int-eq(v, int('1)) then {
      bv <- compileExpBoxing(b)
      after@([bv])
    } else {
      cv <- compileExpBoxing(c)
      after@([cv])
    }
  } label after/1:
  
  compileExp(Var(x)) = {
    read-var!(x)
  }
  
  compileExp(a@NONE()) = compileLiteral(a)
  compileExp(a@True()) = compileLiteral(a)
  compileExp(a@False()) = compileLiteral(a)
  compileExp(a@Int(_)) = compileLiteral(a)
  compileExp(a@String(_)) = compileLiteral(a)
  
  compileExp(List([])) = #array-new([])
  compileExp(List(a)) = {
    av <- compileExpsBoxing(a)
    #array-new(av)
  }
  
  compileExp(m@Member(a, b)) = {
    mem <- compileExp(a)
    assertNotNone(mem)
    using scope!(m, mem) {
      read-var!(b)
    }
  }
  
  compileExp(e@Index(a, _)) = compileIndex(type(a), e)
  
  compileIndex :: List('TYPE) * 'Exp -> Pluggable(@cval)
  compileIndex([LIST(_)|_], Index(a, b)) = { // array index
    av <- compileExp(a)
    assertNotNone(av)
    bv <- compileExp(b)
    assertArrayWithinBounds(av, bv)
    #array-read(av, bv)
  }
  compileIndex(_, Index(a, b)) = { // string index
    av <- compileExp(a)
    idx <- compileExp(b)
    assertStringWithinBounds(av, idx)
    #str-index(av, idx)
  }
  
  compileExp(Call(Var(name), b)) = {
    bv <- compileExpsBoxing(b)
    fn <- read-var!(name)
    fn@(bv ++ [return, $raise])
  } label return/1:
  
  compileExp(Call(m@Member(a, b), args)) = {
    mem <- compileExp(a)
    assertNotNone(mem)
    fn <- using scope!(m, mem)
      read-var!(b)
    
    argv <- compileExpsBoxing(args)
    fn@(argv ++ [return, $raise])
  } label return/1:
  
  compileExp(Negative(a)) = {
    av <- compileExp(a)
    #int-neg(av)
  }
  
  // addition can either be integer or string
  // addition so check the type of the expression
  compileExp(a@Add(_, _)) = compileAdd(type(a), a)
  compileAdd :: List('TYPE) * 'Exp -> Pluggable(@cval)
  compileAdd([INT()], Add(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    #int-add(av, bv)
  }
  compileAdd([LIST(_)], Add(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    assertNotNone(av)
    assertNotNone(bv)
    #array-concat(av, bv)
  }
  compileAdd([_], Add(a, b)) = { // must be string if others didn't match
    av <- compileExp(a)
    bv <- compileExp(b)
    #str-add(av, bv)
  }
  
  compileExp(Sub(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    #int-sub(av, bv)
  }
  compileExp(Mul(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    #int-mul(av, bv)
  }
  compileExp(IntDiv(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    assertDivNotZero(bv)
    #int-div(av, bv)
  }
  compileExp(Mod(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    assertDivNotZero(bv)
    mod(av, bv)
  }
  
  // todo: int-eq only works for ints (obviously)
  compileExp(Eq(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-eq(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:
  compileExp(Neq(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-eq(av, bv) then after@([int('0)])
    else { after@([int('1)]) }
  } label after/1:
  
  compileExp(Lt(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-lt(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:
  compileExp(Gt(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-gt(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:
  compileExp(Geq(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-geq(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:
  compileExp(Leq(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #int-leq(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:
  compileExp(Is(a, b)) = {
    av <- compileExp(a)
    bv <- compileExp(b)
    if #term-eq(av, bv) then after@([int('1)])
    else { after@([int('0)]) }
  } label after/1:

rules
  compileLiteral :: 'Literal -> Pluggable(@cval)
  compileLiteral(NONE()) = null
  compileLiteral(True()) = int('1)
  compileLiteral(False()) = int('0)
  compileLiteral(Int(i)) = int(i)
  compileLiteral(String(STRINGLITERAL(s))) = str(s)
  
  compileLiteralBoxing :: 'Literal -> Pluggable(@cval)
  compileLiteralBoxing(l) = {
    compiled <- compileLiteral(l)
    compileExpBoxingHelper(box(l), boxscope(l), l, compiled)
  }
  
rules
  compileExpBoxing :: 'Exp -> Pluggable(@cval)
  compileExpBoxing(e) = {
    compiled <- compileExp(e)
    compileExpBoxingHelper(box(e), boxscope(e), e, compiled)
  }

  compileExpBoxingHelper :: List('string) * List('Scope) * 'Exp * @cval -> Pluggable(@cval)
  compileExpBoxingHelper([], _, _, e) = e
  compileExpBoxingHelper([_], _, _, e) = e
  compileExpBoxingHelper(["class"|_], [s], e, v) = {
    using scope!(s, v) {
      read-scope!(e)
    }
  }
  compileExpBoxingHelper(["int"|_], _, _, e) = boxInt(e)
  compileExpBoxingHelper(["str"|_], _, _, e) = boxString(e)
  compileExpBoxingHelper(["bool"|_], _, _, e) = boxBool(e)
  compileExpBoxingHelper(["list"|_], _, _, e) = boxList(e)
  
rules
  compileExps :: List('Exp) -> Pluggable(List(@cval))
  compileExps([]) = []
  compileExps([h|tl]) = {
    eh <- compileExp(h)
    etl <- compileExps(tl)
    [eh|etl]
  }
  
  compileExpsBoxing :: List('Exp) -> Pluggable(List(@cval))
  compileExpsBoxing([]) = []
  compileExpsBoxing([h|tl]) = {
    eh <- compileExpBoxing(h)
    etl <- compileExpsBoxing(tl)
    [eh|etl]
  }